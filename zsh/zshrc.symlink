# .zshrc
# ==============================================================================

# This script is run whenever an interactive zsh shell is started

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# all of our zsh files
typeset -U configFiles
configFiles=($ZSH/*/*.zsh) # Using $ZSH/*/*.zsh here decreases startup time significantly over $ZSH/**/*.zsh

# load the path files
for file in ${(M)configFiles:#*/path.zsh}; do
	source $file
done

# load everything but the path and completion files
for file in ${${configFiles:#*/path.zsh}:#*/completion.zsh}; do
	source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)configFiles:#*/completion.zsh}; do
	source $file
done

unset configFiles

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

eval "$(direnv hook zsh)"
